to create-uninfected-people
  create-people (population - infected-quantity)[
    set gender a_new_gender
    set shape "person"
    set age age_probability
    set district (random 19) + 1 
    set vulnerability_index 0.2
    
    set place_of_care "none"
    ; set mobility "" I'm not sure what values could have this field
    set status "uninfected"
    set level_of_infection "none"
    set time_infection 0
    set previous_medical_condition "none"
    set color white
    set size 1.5
    set xcor random-xcor
    set ycor random-ycor
    set viral_number 0
    set latency-time 0
    set infected_someone false
  ]
end

to create-infected-people [quantity]
  create-people quantity [
    set gender a_new_gender
    set shape "person"
    set age age_probability
    set district (random 19) + 1 
    set vulnerability_index 0.2
    set level_of_infection "asymptomatic"
    set place_of_care assign_place_of_care level_of_infection
    ; set mobility "" I'm not sure what values could have this field
    set status "infected"
    set time_infection 1
    set previous_medical_condition "none"
    
    set color red
    set size 1.5
    set xcor random-xcor
    set ycor random-ycor
    set viral_number 1
    set latency-time latency-period
    set infected_someone false
  ]
end

to step-people
  infected-people
  move-people
  delete-immnunity
end

to move-people
  ifelse (not-move? = true)[
    ifelse (rule-ages = true)[
      ask people with[(place_of_care = "none") and (age > min-age-to-go-outside) and (age < max-age-to-go-outside)] [fd 1]
    ][
      ask people with[place_of_care = "none"] [fd 1]
    ]
  ][
    ifelse (rule-ages = true)[
      ask people with[(place_of_care = "none") and (age > min-age-to-go-outside) and (age < max-age-to-go-outside)] [fd 1]
    ][
      ask people [fd 1]
    ]
  ]
end

to delete-immnunity
  ask people with[status = "recovered"] [
    set personal-immunity-time personal-immunity-time - 1
    if (personal-immunity-time <= 0)[
      set status "uninfected"
      set viral_number 0
      set color white
    ]
  ]
end

to infected-people
  ask people [
    right (random 360)
    
    ;; The idea is if the person is infected can infected to other people    
    if (status = "infected")[
      if (latency-time = 0)[
        let infected who
        ;; Increate the time of infection
        set time_infection time_infection + 1
        
        ;; Only take in mind the people that is near
        ask people-here[
          let max_distance 2 / patch-meters
          if (distance myself <= max_distance) and (myself != self) and (status != "infected") and (status != "recovered")[
            ask self[
              infect-person infected
            ]
          ]
        ]
        ;; Evaluate if healing person
        healing_person 
        ;; Evaluate if person die 
        die_for_not_healing
      ]
      ;; Update status of illness
      set level_of_infection illness_evolution level_of_infection time_infection
      ;; Update place of care
      set place_of_care assign_place_of_care level_of_infection
    ]
    
    update_time_infection
  ]
end

to infect-person[ infected]
  set status "infected"
  set level_of_infection "asymptomatic"
  set place_of_care assign_place_of_care level_of_infection
  set time_infection 1
  set previous_medical_condition "none"
  set viral_number 1
  set color red
  set latency-time latency-period
  set infected_someone false
  if show-who-infected-who = true [
    create-link-with person infected
  ]
end

to update_time_infection
  if (latency-time > 0)[ set latency-time latency-time - 1 ]
  set time_infection time_infection + 1
end

to healing_person
  if ((min-time-infection-level level_of_infection) <= time_infection)[
      set status "recovered"
      set personal-immunity-time immunity-time * 24
      set level_of_infection "none"
      set place_of_care "none"
      set color green
  ]
end

to die_for_not_healing
  let probability_die random-float 1 
  
  if ((level_of_infection = "serious") and (probability_die <= 0.0003)) [
    set number-of-deaths number-of-deaths + 1
    die
  ] 
end